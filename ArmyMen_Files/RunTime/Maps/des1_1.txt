//
//	DESERT 
//	Map #1
//	Mission #1
//
////////////////////////


//PAD
PAD GreenBase		30		Green		> 0		//Green Units that will hold position
PAD TanAttack		15		Tan		> 0		//Tan Attackers on GreenBase 
PAD GreenDefense	10		Green		> 0		//GreenSoldier1
PAD GreenDefense2	20		Green		> 0
PAD BlueEscape		40		Blue		> 0
PAD TBase		60		TanSoldierR1	> 0
PAD TBase2		16		Tan		> 0
PAD TBase3		70		Tan		> 0	
PAD TASPOT		17		Tan		> 0
PAD GTent		50		Green		> 0
PAD RadioT		80		Tan		> 0
PAD RadioBase		75		Tan		> 0
PAD GreenDefense3	72		Green		> 0
PAD DEADZONE		78		Green		> 0
	

//ENDGAMES
IF GREENWIN THEN
	INVULNERABLESARGE,
	TRIGGERDELAY 1 greenwins

IF TANWIN THEN
	INVULNERABLESARGE,
	TRIGGERDELAY 1 tanwins
	

// WIN CON
IF killed_TanRifle3 THEN
	TRIGGER GREENWIN

//LOSE CON
IF killed_GreenSarge THEN
	TRIGGER TANWIN

IF killed_BlueEscort1 THEN
	TRIGGER TANWIN

IF killed_BlueEscort2 THEN
	TRIGGER TANWIN

IF killed_BlueEscort3 THEN
	TRIGGER TANWIN
	
//INIT
IF startup THEN
	VULNERABLESARGE,
	ALLY TAN BLUE,
	ALLY GREEN BLUE,
	ALLY GREEN GRAY,
	TRIGGER ALLYG,
	ORDER TFlameboi1 GOTO GreenSarge INMODE ATTACK THEN ATMODE ATTACK
	

//BLUE POWS

IF killed_TFlameboi1 THEN
	REMOVEOBJECT Bazooka1,
	PLACEOBJECT Bazooka1 TFlameboi1,
	TRIGGER TSTORM,
	TRIGGER TRESPAWN,
	TRIGGERDELAY 8 PRIS1

IF PRIS1 THEN
	DEPLOY BlueEscort1 ATMODE ATTACK,
	SETAICONTROL BlueEscort1,
	ORDER BlueEscort1 GOTO BTruck1 INMODE DEFEND THEN TRIGGER TANATTACK1

IF killed_TanRifle7 THEN
	DEPLOY BlueEscort2 ATMODE ATTACK,
	SETAICONTROL BlueEscort2,
	ORDER BlueEscort2 GOTO BTruck1 INMODE DEFEND THEN TRIGGER TANATTACK2

IF killed_TanFlamer2 THEN
	DEPLOY BlueEscort3 ATMODE ATTACK,
	SETAICONTROL BlueEscort3,
	ORDER BlueEscort3 GOTO BTruck1 INMODE DEFEND THEN TRIGGER BLUEBACKUP

//TRIGGERS

IF ALLYG THEN
	SETAICONTROL GreenRifle1,
	SETAICONTROL GreenRifle2,
	SETAICONTROL GreenRifle4,
	SETAICONTROL GreenRifle5,
	SETAICONTROL GDefender1,
	SETAICONTROL GDefender2

IF TANATTACK1 THEN
	UNDEPLOY BlueEscort1,
	SETAICONTROL TanRifle1,
	ORDER TanRifle1 GOTO GreenBase INMODE ATTACK THEN ATMODE ATTACK,
	SETAICONTROL TanRifle2,
	ORDER TanRifle2 GOTO GreenBase INMODE ATTACK THEN ATMODE ATTACK,
	DEPLOY TanRifle7 ATMODE ATTACK,
	SETAICONTROL TanRifle7,
	ORDER TanRifle7 GOTO GreenBase INMODE ATTACK THEN ATMODE ATTACK

IF TANATTACK2 THEN
	UNDEPLOY BlueEscort2,
	DEPLOY TanJeep1 ATMODE ATTACK,
	SETAICONTROL TanJeep1,
	ORDER TanJeep1 GOTO GreenBase INMODE DEFEND THEN ATMODE DEFEND,
	SETAICONTROL TanRifle8,
	ORDER TanRifle8 GOTO GreenBase INMODE ATTACK THEN ATMODE ATTACK,
	SETAICONTROL TanFlamer2,
	ORDER TanFlamer2 GOTO GreenBase INMODE ATTACK THEN ATMODE ATTACK

	

IF BLUEBACKUP THEN
	UNDEPLOY BlueEscort3,
	TRIGGER GTROOPERS

//TAN RESPAWN UNITS

IF TRESPAWN THEN
	DEPLOY TanSoldierR1 ATMODE ATTACK,
	ORDER TanSoldierR1  GOTO GreenBase INMODE ATTACK THEN ATMODE ATTACK

IF ALLOF killed_TanSoldierR1 THEN
	TRIGGERDELAY 10 TRES1

IF TRES1 THEN
	RESURRECT TanSoldierR1 TBase,
	TRIGGER TRESPAWN

IF ALLOF killed_TanSoldierR2 THEN
	TRIGGERDELAY 10 TRES2

IF TRES2 THEN
	RESURRECT TanSoldierR2 TBase2,
	ORDER TanSoldierR2  GOTO TASPOT INMODE ATTACK THEN ATMODE ATTACK
	
//GREEN RESPAWN UNITS

IF ALLOF killed_GDefender1 THEN
	TRIGGERDELAY 6 GRES1

IF ALLOF killed_GDefender2 THEN
	TRIGGERDELAY 6 GRES2

IF GRES1 THEN
	RESURRECT GDefender1 GTent,
	ORDER GDefender1 GOTO GreenDefense INMODE ATTACK THEN ATMODE ATTACK

IF GRES2 THEN
	RESURRECT GDefender2 GTent,
	ORDER GDefender2 GOTO GreenDefense2 INMODE ATTACK THEN ATMODE ATTACK
	
IF get_GreenSarge_HELLO1 THEN
	SHOWBMP "D12mid.bmp",
	TRIGGER GREYFRIENDS

IF GREYFRIENDS THEN
	DEPLOY GrayRifle1 ATMODE DEFEND,
	SETUSERCONTROL GrayRifle1
	

//WIN CON
IF GTROOPERS THEN
	DEPLOY BlueS1 ATMODE ATTACK,
	ORDER BlueS1 GOTO BTruck1 INMODE DEFEND THEN TRIGGER UNDEPLOYBS1,
	DEPLOY GreenRein1 ATMODE DEFEND,
	SETAICONTROL GreenRein1,
	ORDER GreenRein1 GOTO TBase3 INMODE ATTACK THEN ATMODE ATTACK,
	DEPLOY GTank1 ATMODE ATTACK,
	SETAICONTROL GTank1,
	ORDER GTank1 GOTO GreenBase THEN TRIGGER UNDEPLOYEDB

IF UNDEPLOYBS1 THEN
	UNDEPLOY BlueS1
	
	
IF UNDEPLOYEDB THEN
	DEPLOY GCol1 ATMODE ATTACK,
	ORDER GCol1 GOTO GreenSarge INMODE DEFEND THEN ATMODE ATTACK,
	SETAICONTROL GCol1,
	SETUSERCONTROL	GTank1,
	SETAICONTROL 	BTruck1,
	ORDER BTruck1 GOTO BlueEscape THEN TRIGGER UNDEPLOYEDJ

IF UNDEPLOYEDJ THEN
	UNDEPLOY BTruck1,
	TRIGGER GREENWIN

	
	
	

	

	




	



